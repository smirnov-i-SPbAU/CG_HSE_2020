﻿// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel ComputeHarmonics

#define PI            3.14159265359f
#define TWO_PI        6.28318530718f
#define FOUR_PI       12.56637061436f
#define INV_PI        0.31830988618f
#define INV_TWO_PI    0.15915494309f
#define INV_FOUR_PI   0.07957747155f
#define HALF_PI       1.57079632679f
#define INV_HALF_PI   0.636619772367f

RWStructuredBuffer<float4> SH_0_1_r;
RWStructuredBuffer<float4> SH_0_1_g;
RWStructuredBuffer<float4> SH_0_1_b;

RWStructuredBuffer<float4> SH_2_r;
RWStructuredBuffer<float4> SH_2_g;
RWStructuredBuffer<float4> SH_2_b;

RWStructuredBuffer<float4> SH_2_rgb;

TextureCube<half4> _Env;
SamplerState samplerLinearRepeat;

uint Hash(uint s)
{
    s ^= 2747636419u;
    s *= 2654435769u;
    s ^= s >> 16;
    s *= 2654435769u;
    s ^= s >> 16;
    s *= 2654435769u;
    return s;
}

float Random(uint seed)
{
    return float(Hash(seed)) / 4294967295.0; // 2^32-1
}

float3 SampleColor(float3 direction)
{   
    float4 tex = _Env.SampleLevel(samplerLinearRepeat, direction, 0);
    return tex.rgb;
}

[numthreads(1024, 1, 1)]
void ComputeHarmonics(uint id : SV_DispatchThreadID)
{ 
    const int THREADS = 1024;
    const int SAMPLES = 100;
    
    // See SH.shader for more details
    float4 results[7] = {
        float4(0, 0, 0, 0), // SH_0_1_r
        float4(0, 0, 0, 0), // SH_0_1_g
        float4(0, 0, 0, 0), // SH_0_1_b
        
        float4(0, 0, 0, 0), // SH_2_r
        float4(0, 0, 0, 0), // SH_2_g
        float4(0, 0, 0, 0), // SH_2_b
        
        float4(0, 0, 0, 0), // SH_2_rgb
    };

    const float const_y00 = 1.0; // 1/(2*sqrt(PI)) ** 2 * 4PI * PI / PI
    
    const float const_y1_1 = 2.0; // (-sqrt(3)/(2*sqrt(PI))) ** 2 * 4 * PI / PI * 2PI/3 
    const float const_y10 = 2.0; // (sqrt(3)/(2*sqrt(PI))) ** 2 * 4 * PI / PI * 2PI/3 
    const float const_y11 = 2.0; // (-sqrt(3)/(2*sqrt(PI))) ** 2 * 4 * PI / PI * 2PI/3 
    
    const float const_y2_2 = 15 / 4.0; // (sqrt(15)/(2*sqrt(PI)) ** 2 * 4 * PI / PI * PI / 4
    const float const_y2_1 = 15 / 4.0; // (sqrt(15)/(2*sqrt(PI)) ** 2 * 4 * PI / PI * PI / 4
    const float const_y20 = 5 / 8.0; // (sqrt(5)/(4*sqrt(PI)) ** 2 * 4 * PI / PI * PI / 4 * 2 (will be divided by 2 on shader)
    const float const_y21 = 15 / 4.0; // (sqrt(15)/(2*sqrt(PI)) ** 2 * 4 * PI / PI * PI / 4
    const float const_y22 = 15 / 16.0; // (sqrt(15)/(4*sqrt(PI)) ** 2 * 4 * PI / PI * PI / 4

    for (uint i = SAMPLES * id; i < SAMPLES * (id + 1); ++i)
    {
        float cos_theta = 2 * Random(2 * i) - 1;
        float sin_theta = sqrt(1 - cos_theta * cos_theta);
        float alpha = 2 * PI * Random(2 * i + 1);
        float3 w = float3(sin_theta * cos(alpha), sin_theta * sin(alpha), cos_theta);
        float3 color = SampleColor(w);

        float y00 = 1;
        float y1_1 = w.y;
        float y10 = w.z;
        float y11 = w.x;
        float y2_2 = w.y * w.x;
        float y2_1 = w.y * w.z;
        float y20 = 3 * w.z * w.z - 1;
        float y21 = w.x * w.z;
        float y22 = w.x * w.x - w.y * w.y;

        for (int j = 0; j < 3; ++j)
        {
            results[j].x += y11 * color[j];
            results[j].y += y1_1 * color[j];
            results[j].z += y10 * color[j];
            results[j].w += y00 * color[j];
        }

        for (int k = 0; k < 3; ++k)
        {
            results[k + 3].x += y2_2 * color[k];
            results[k + 3].y += y2_1 * color[k];
            results[k + 3].z += y21 * color[k];
            results[k + 3].w += y20 * color[k];
        }
        results[6].x += y22 * color[0];
        results[6].y += y22 * color[1];
        results[6].z += y22 * color[2];
    }

    for (int result_id = 0; result_id < 7; ++result_id)
    {
        results[result_id] /= SAMPLES;
    }

    for (int j = 0; j < 3; ++j)
    {
        results[j].x *= const_y11;
        results[j].y *= const_y1_1;
        results[j].z *= const_y10;
        results[j].w *= const_y00;
    }
    
    for (int k = 0; k < 3; ++k)
    {
        results[k + 3].x *= const_y2_2;
        results[k + 3].y *= const_y2_1;
        results[k + 3].z *= const_y21;
        results[k + 3].w *= const_y20;
    }
    results[6] *= const_y22;
    
    // Populate results buffer using monte-carlo method.
    // You can see the Random(i) function. Use it just as with the previous task.
    
    // TASK: project light from _Env to spherical harmonic coefficients and store them in results[]
    
    SH_0_1_r[id] = results[0];
    SH_0_1_g[id] = results[1];
    SH_0_1_b[id] = results[2];
    
    SH_2_r[id] = results[3];
    SH_2_g[id] = results[4];
    SH_2_b[id] = results[5];    
    
    SH_2_rgb[id] = results[6];
}